# SPDX-License-Identifier: MIT

:init
  push $(end - code)
  dup %1
  push $(code)
  push 0
  codecopy
  push 0
  return

:code
  # The CREATE2 factory expects calldata to be a 32-byte CREATE2 salt value
  # followed by the contract init code.

  # First, load the salt value and compute the actual code size for the CREATE2
  # call, this is the calldata length minus 32 for the salt prefix.
  push 0
  calldataload
  push 32
  calldatasize
  sub

  # Copy the init code to memory offset 0.
  ## STACK: [ (calldatasize - 32) ; salt ]
  dup %1
  push 32
  push 0
  calldatacopy

  # Deploy the contract
  ## STACK:  [ (calldatasize - 32) ; salt ]
  ## MEMORY: [ calldata[32:] ]
  push 0
  callvalue
  create2

  # Verify the deployment was successful and return the address.
  ## STACK: [ address ]
  dup %1
  push $(success)
  jumpi
:revert
  ## STACK: [ 0 ]
  returndatasize
  push 0
  push 0
  returndatacopy
  push 0
  revert
:success
  ## STACK: [ address ]
  jumpdest
  push 0
  mstore
  push 32
  push 0
  return

:end
